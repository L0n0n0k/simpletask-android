buildscript {

    ext.kotlin_version = '1.0.1'
    ext.android_support_version = '23.3.0'
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0'
	    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.3.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
// apply plugin: 'com.getkeepsafe.dexcount'


android {
    buildToolsVersion "23.0.3"
    compileSdkVersion 23

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }


    defaultConfig {
        versionName '7.2.2'
        versionCode 3031
	    minSdkVersion 14
        targetSdkVersion 23
    }

    productFlavors {
        dropbox {
            applicationId 'nl.mpcjanssen.todotxtholo'
        }
        cloudless {
            applicationId 'nl.mpcjanssen.simpletask'
        }
        //toodledo {
        //    applicationId 'nl.mpcjanssen.simpletask.toodledo'
        //}
    }

    signingConfigs {
        release {
            keyAlias = "mpcjanssen.nl"
        }
    }

    buildTypes {
        release {
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
	    minifyEnabled true
            proguardFile "proguard.txt"
        }
    }
    dexOptions {
        incremental = false
        preDexLibraries = false
    }
}

repositories {
    jcenter()
}

dependencies {
    dropboxCompile fileTree(dir: 'libs', include: '*.jar')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.android.support:support-v4:$android_support_version"
    compile "com.android.support:design:$android_support_version"
    compile "com.android.support:appcompat-v7:$android_support_version"
    compile "de.greenrobot:greendao:2.1.0"
    compile 'com.github.rjeschke:txtmark:0.11'
    compile 'com.darwinsys:hirondelle-date4j:1.5.1'
    testCompile 'junit:junit:4.12'
}

// Only sign if we have keystore properties in the gradle.properties in ~/.gradle
if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}
